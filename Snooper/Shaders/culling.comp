layout(local_size_x = 64) in;

struct PrimitiveDescriptor
{
    vec3 center;
    vec3 extents;
    uint maxLod;
    uvec4 firstIndex[2];
    uvec4 baseVertex[2];
    uvec4 sectionCount[2];
    uvec4 sectionOffsets[2];
};

struct SectionDescriptor
{
    uint firstIndex;
    uint count;
    uint materialIndex;
};

struct PerInstanceData
{
    mat4 matrix;
};

struct DrawElementsIndirectCommand
{
    uint count;
    uint instanceCount;
    uint firstIndex;
    uint baseVertex;
    uint baseInstance;
    uint originalInstanceCount;
    uint originalBaseInstance;
    uint modelId;
    uint sectionId;
};

layout(std430, binding = 0) readonly buffer PerInstanceDataBuffer {
    PerInstanceData uInstanceDataBuffer[];
};

layout(std430, binding = 1) readonly buffer DescriptorsBuffer {
    PrimitiveDescriptor uDescriptors[];
};

layout(std430, binding = 2) buffer DrawCommandBuffer {
    DrawElementsIndirectCommand uIndirectDrawCommands[];
};

layout(std430, binding = 3) buffer SectionDescriptorsBuffer {
    SectionDescriptor uSectionDescriptors[];
};

uniform vec4 uFrustumPlanes[6];
uniform vec3 uCameraPosition;

bool isVisible(vec3 center, vec3 extents)
{
    bool isInside = true;
    for (int i = 0; i < 6; ++i)
    {
        vec4 plane = uFrustumPlanes[i];
        vec3 normal = plane.xyz;
        float d = plane.w;
        
        float r = dot(abs(normal), extents);
        float s = dot(normal, center) + d;
        if (s < -r)
        {
            isInside = false;
            break;
        }
    }
    
    return isInside;
}

void main()
{
    uint drawId = gl_GlobalInvocationID.x;
    if (drawId >= uIndirectDrawCommands.length()) return;

    DrawElementsIndirectCommand cmd = uIndirectDrawCommands[drawId];
    uint instanceCount = cmd.originalInstanceCount;
    uint baseInstance = cmd.originalBaseInstance;

    PrimitiveDescriptor descriptor = uDescriptors[cmd.modelId];

    uint minVisible = 0xFFFFFFFFu;
    uint maxVisible = 0;
    float closestDistance = 3.402823e+38;
    
    for (uint j = 0; j < instanceCount; ++j)
    {
        mat4 modelMatrix = uInstanceDataBuffer[baseInstance + j].matrix;

        vec3 worldCenter = (modelMatrix * vec4(descriptor.center, 1.0)).xyz;
        mat3 m = mat3(modelMatrix);
        vec3 worldExtents = vec3(
            dot(abs(m[0]), descriptor.extents),
            dot(abs(m[1]), descriptor.extents),
            dot(abs(m[2]), descriptor.extents)
        );

        if (isVisible(worldCenter, worldExtents))
        {
            minVisible = min(minVisible, j);
            maxVisible = max(maxVisible, j);

            float dist = length(uCameraPosition - worldCenter);
            if (dist < closestDistance)
            {
                closestDistance = dist;
            }
        }
    }

    if (minVisible <= maxVisible)
    {
        uint newBaseInstance = baseInstance + minVisible;
        uint newInstanceCount = maxVisible - minVisible + 1;

        if (cmd.baseInstance != newBaseInstance || cmd.instanceCount != newInstanceCount)
        {
            uIndirectDrawCommands[drawId].baseInstance = newBaseInstance;
            uIndirectDrawCommands[drawId].instanceCount = newInstanceCount;
        }

        uint lod = 0;
        if (closestDistance < 5.0)
            lod = 0;
        else if (closestDistance < 10.0)
            lod = 1;
        else if (closestDistance < 15.0)
            lod = 2;
        else
            lod = 3;

        lod = clamp(lod, 0u, descriptor.maxLod);
        uint lodGroup = lod / 4;
        uint lodSub = lod % 4;

        uint sectionCountThisLod = descriptor.sectionCount[lodGroup][lodSub];
        if (cmd.sectionId >= sectionCountThisLod)
        {
            uIndirectDrawCommands[drawId].instanceCount = 0;
            return;
        }
        
        uint sectionIndex = descriptor.sectionOffsets[lodGroup][lodSub] + cmd.sectionId;
        uIndirectDrawCommands[drawId].count = uSectionDescriptors[sectionIndex].count;
        uIndirectDrawCommands[drawId].firstIndex = descriptor.firstIndex[lodGroup][lodSub] + uSectionDescriptors[sectionIndex].firstIndex;
        uIndirectDrawCommands[drawId].baseVertex = descriptor.baseVertex[lodGroup][lodSub];
    }
    else if (cmd.instanceCount != 0)
    {
        uIndirectDrawCommands[drawId].instanceCount = 0;
    }
}
